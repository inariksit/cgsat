DELIMITERS = "<.>" "<..>" "<...>" "<!>" "<?>" "<Â¶>" sent ; 
SOFT-DELIMITERS = "<,>" ;

LIST BOS = (>>>) ; 
LIST EOS = (<<<) ; 

SETS

LIST N = n ; 
LIST Det = det ; 
LIST A = adj ; 
LIST V = vblex vbser vaux vbhaver vbdo ; 
LIST Adv = adv ; 
LIST Pron = prn ; 
LIST CS = cnjsub ; 
LIST CC = cnjcoo ; 
LIST Rel = rel ; 
LIST Preadv = preadv ; 
LIST Pr = pr ; 
LIST NUM = num ;

LIST Cop = vbser ; 

LIST Inf = inf ; 
LIST Pres = pres pri; 
LIST Past = past ;  
LIST Ger = ger ; 
LIST VerbalNoun = (vblex subs) ;
LIST PPRS = (vblex pprs); 
LIST PrcPast = pp ; 
LIST AdvItg = (adv itg) ; 

LIST Interr = itg ; 
LIST Subj = subj ; 
LIST Obj = obj ; 
LIST Pos = pos ; 

LIST Months = "January" "February" "March" "April" "May" "June" "July" "August" "September" "October" "November" "December" ;
LIST Seasons = "winter" "spring" "summer" "autumn" ;
LIST Year = year ; 

LIST 1p = p1 ;
LIST 2p = p2 ;
LIST 3p = p3 ;

LIST FIN = pres past pri ; 

LIST SENT = sent ; 

SET V-FIN = V + FIN ;

SET V-DO = ("do"i) ; 

SET English-sg = ("English"i) ;

SET VAUX-CAN = ("can"i) ;

SET VAUX-WILL = ("will"i) ;

SET VAUX-WOULD = ("would"i) ;

SET Quest = ("<?>") ;

SET V-DO-AUX = (vbdo) ;

SET ADV-NOT = ("not") ;
 
SET V-HAVE = (vbhaver) ; 

SET V-PRES = (vblex pres) ; 

SET V-MOD = ("must") | ("should") | ("will") | ("can") ; 

LIST PRN-SUBJ  = (prn subj) ;

SET CLB = SENT | V-FIN | Subj ; # 'Subj' is risky, e.g. coordination

SET PERSON = 1p | 2p | 3p ;

SECTION

REMOVE:r01 V-FIN IF (-1C V-FIN);  # Careful: would kill "The man who _came_ _wrote_ a book"

REMOVE:r02 N IF (0 ("<is>")) ; 

SELECT:s01 N IF (-1 ("<both>")) ; 

SELECT:s02 V IF (0 ("<like>")) ; 

SELECT:s03 N IF (0 ("<names>"))(-1 Pr) ; 

SELECT:s04 (vblex pp) IF (0 ("<hit>")) (-1 ("<is>")) ; 

SELECT:s05 (vblex sep pp) IF (-1 (vbser past)) ; 

REMOVE:r03 N IF (0 ("<will>"))(1 ADV-NOT);

SELECT:s06 (vbhaver inf) if (0 ("have")) (-1 ("not"));

SELECT:s07 (n sg) if (0 ("English")) ;

SELECT:s08 (n sg) if (0 ("Kazakh")) ;

SELECT:s09 (vblex pp) if (-1 (vbser past)) ;

SELECT:s10 (vblex inf) if (0 ("be")) (-1 Pr) ;

SELECT:s11 (vblex ger) if (0 ("beginning"))(NOT 1 V) ;

SELECT:s12 Rel if (-1 N) ;

#SELECT N if (-1 Pr) ;

SELECT:s13 A if (1C ("Party")) ;

SELECT:s14 AdvItg if (1C V) ;

SELECT:s15 (det dem) if (1 N) ;

SELECT:s16 AdvItg if (2C V) ;

#SELECT (vblex) if (-1 ("was")) ;

SELECT:s17 N if (-1 AdvItg) ;

#SELECT A if (1 ("Autonomous")) ;

SELECT:s18 N if (-1 Det) (NOT 0 A) ;

SELECT:s19 V-HAVE if (2C V) ;

SELECT:s20 Inf if (-2 V-MOD) (-1 ADV-NOT);

SELECT:s21 Inf if (-1 VAUX-WOULD) ;

SELECT:s22 Pr if (-1 VAUX-WOULD) ;

SELECT:s23 Inf IF (-2 V-FIN) (-1 ("<to>"));

SELECT:s24 Inf IF (0 ("<will>")) (1 ADV-NOT);

SELECT:s25 V-HAVE IF (1 PrcPast);

SELECT:s26 V IF (1 Ger)(NOT 0 (vblex pres p3 sg));

REMOVE:r04 V-FIN IF (-1 A) (-2 Det); 

REMOVE:r05 V-FIN IF (-1 Det);

REMOVE:r06 Ger IF (-1 V-FIN) (NOT -1 Cop) (1 N); #MLF not sure about this rule here    

REMOVE:r07 A IF (-1 Det) (0 N OR A) (1 ("<is>")) (2 A);

REMOVE:r08 Inf IF (0 N OR Inf) (1 ("to")) (2 Inf);

SELECT:s27 V IF (1 ("to")) (2 Inf);

SELECT:s28 N IF (-1C NUM);

SELECT:s29 Inf IF (-1 ADV-NOT)(-2 VAUX-CAN);

SELECT:s30 N IF (-1 ("Baltic"));

SELECT:s31 N IF (-1 ("Asian"));

SELECT:s32 N IF (-1 A);

SELECT:s33 Adv IF (-1 V)(0 ("forward"));

SELECT:s34 Inf IF (-1 V-MOD);

SELECT:s35 N IF (1 Pr)(2 N);

REMOVE:r09 N IF (0 V-FIN) (0 N) (0 N OR V-FIN) ((NOT 1* V-FIN BARRIER CLB) OR (NOT -1C* V-FIN BARRIER CLB));

REMOVE:r10 A IF (0 A OR Adv) (NOT 1 N) (NOT -1* Cop BARRIER CLB) ;

REMOVE:r11 N IF (0 N OR V-FIN) (1 ("<to>")) (2 Inf);

SELECT:s36 V-FIN + $$PERSON IF (-1 Subj + $$PERSON);

SELECT:s37 CS IF (-1 V)(1 N OR PRN-SUBJ);

SELECT:s38 Inf IF (-1* V-DO BARRIER SENT) (NOT 1 Det);

REMOVE:r12 Inf IF (NOT 0 ("will") ) (1C ADV-NOT) (2 Inf);

SELECT:s39 Pr IF (1 N);

SELECT:s40 Pr IF (1 NUM);

SELECT:s41 Inf IF (1 Inf) (2 Ger);

SELECT:s42 Inf IF (1 Inf) (2 PrcPast);

SELECT:s43 Inf IF ((-1 ADV-NOT) OR (0 ADV-NOT)) ((-1 VAUX-CAN) OR (1 VAUX-CAN));

SELECT:s44 V-PRES IF (-1 N OR Pron)(1 N OR Pron);

SELECT:s45 V-DO-AUX IF (NOT 1C ADV-NOT);

SELECT:s46 V-DO-AUX IF (-1 PRN-SUBJ) (1C ADV-NOT);

REMOVE:r13 V-DO-AUX IF (-1 PRN-SUBJ) (NOT 1C ADV-NOT);

SELECT:s47 (gen) IF (0 ("'s"));

SELECT:s48 V IF (-1 PRN-SUBJ OR N);

SELECT:s49 V-HAVE IF (1C ADV-NOT) (2 PrcPast);

SELECT:s50 Inf IF (0 Inf) (1 ADV-NOT)(2 Ger);

SELECT:s51 Pres IF (1 ADV-NOT) (2 Inf); 

SELECT:s52 ("put"i vblex pp) IF (-1 V-HAVE); 

SELECT:s53 ("forward"i adv) IF (-1 V); 

REMOVE:r14 Inf IF (-1 PRN-SUBJ) (NOT 0 ("will"));

REMOVE:r15 N IF (-1 PRN-SUBJ) (0 ("will") );

REMOVE:r16 N IF (0 ("will")) (1C ADV-NOT) ;

REMOVE:r17 Inf IF (0 Inf OR V-FIN) (NOT 1* V-FIN BARRIER CLB) (NOT -1* V-FIN BARRIER CLB);

REMOVE:r18 PrcPast IF (-1 VAUX-WILL);

REMOVE:r19 V-FIN IF (0 Inf) (0 Inf OR V-FIN) ((1C* V-FIN BARRIER CLB) OR (-1C* V-FIN BARRIER CLB));

SELECT:s54 Inf IF (-2 ("do")) (-1 Pron) (0 Pres OR Inf);

REMOVE:r20 CS IF (NOT 1* V-FIN BARRIER CLB);

REMOVE:r21 Det IF (NOT 1 N OR A OR NUM);

SELECT:s55 Adv + Interr IF (0 Adv OR Rel) (1* ("?")) ;

SELECT:s56 Subj IF (0 Subj OR Obj) (1 V-FIN);

SELECT:s57 A IF (-1 Preadv) (0 A OR V);

#REMOVE Pr IF (NOT 1* N BARRIER CLB);

REMOVE:r22 Pr IF (0 Pr OR CC) (-1 SENT);

# SELECT V-FIN + $$PERSON (-1C Subj + $$PERSON);

SELECT:s58 Inf IF (-2 V-FIN) (-1 Adv) (0 V-FIN OR Inf);

SELECT:s59 Det + Pos IF (0 Det OR Pron) (1 N);

SELECT:s60 A IF (0 Det OR A) (-1 Det) (1 N);

REMOVE:r23 Ger IF (-1 Det OR A);

SELECT:s61 Ger IF (-1 Cop);  # Would not work with "We are sleeping beauties", but it is hard... 

SELECT:s62 PPRS IF (-1 Det OR A) (1 N);

SELECT:s63 Pr IF (0 Pr OR Adv) (1 Det);

REMOVE:r24 Rel IF (-1 CC);

SELECT:s64 Det IF (0 ("<no>") OR ("<some>")) (1C N);

REMOVE:r25 A IF (0C A OR N) (-1 Det) (1 SENT);

SELECT:s65 Det IF (0C Pron OR Det) (1C N);

SELECT:s66 V-FIN IF (-1C Subj) (1 Inf);

REMOVE:r26 Subj IF (0C Subj OR Obj) (-1C* Subj BARRIER SENT);

SELECT:s67 V-HAVE IF (1C PrcPast);

SELECT:s68 Pr IF (0 Pr OR Adv) (1 Pron + Obj) ;

REMOVE:r27 N IF (0 N OR V) (-1 VAUX-CAN) ;

REMOVE:r28 Pron IF (0 Det OR Pron) (1 NUM) ;

SELECT:s69 Year IF (-1 ("in") OR ("after") OR ("since") OR ("of") OR ("throughout")) (NOT 1 N);

SELECT:s70 Year IF (-1 Seasons OR Months);
 
AFTER-SECTIONS 

REMOVE:r29 Year ; 
