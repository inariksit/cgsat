DELIMITERS = "<.>" "<!>" "<?>" "<...>" "<¶>" "<:>";
SOFT-DELIMITERS = "<,>" "<;>";
SETS
LIST >>> = >>> ;
LIST <<< = <<< ;
LIST N = (n) ;
LIST NP = (np) ;
LIST A = (adj) ;
LIST Num = (num) ;
LIST Adv = (adv) ;
LIST PreAdv = (preadv) ;

# Dets
LIST Det = det detnt (rel aa) ;
LIST Predet = (predet) ;

# Verbs
LIST Vblex = (vblex) ;
LIST Vbmod = (vbmod) ;
LIST Vbhaver = (vbhaver) ;
LIST Copula = (vbser);

LIST Subj = prs pis ;
LIST Imp = (imp) ;
LIST Inf = (inf) ;
LIST Pres = (pri) ;
LIST Past = (pii) ;
LIST Cond = (cni) ;
LIST PastCondSubj = past cond subj ;
SET PastCondSubjP1 = PastCondSubj + (p1)  ;

LIST VerbFin = pri prs imp pii cni pis ifi fti;
LIST VerbInd = pri imp pii ifi fti ;

# Pronoms
LIST Prn = prn ;
SET PrnIndep = (prn pro) OR (prn itg) OR (prn tn) ;
SET Ref = (prn pro ref) ;
SET Pro = (prn pro) ;


# Preposicions
LIST Prep = (pr) ;
LIST Despues = "después de" "Después de" "antes de" "Antes de" "tras" "Tras" ;

# Conjuncions
LIST CnjCoo = (cnjcoo) ;
# quantes vegades s'executa cada regla?
# había sido inventado por los griegos mientras que los españoles aseguraban
# elimina españoles adj, però no griegos adj; si se suprimeix la segona part de la frase, sí se suprimeix griegos adj
LIST CnjSub = (cnjsub) ;
LIST CnjAdv = (cnjadv) ;


LIST Rel = (rel) ;		# PROBLEMA: està malparit en eo-es i eo-ca: prn.rel i rel.adv (es-ca: rel.adv, rel.an, rel.nn, rel.aa)
LIST Interj = (ij) ;
SET CM = (cm) OR (lpar) OR (rpar) OR ("<;>") ;
SET Verb = Vblex OR Vbmod OR Vbhaver OR Copula ;
SET Word = N OR NP OR A OR Det OR PreAdv OR Adv OR Verb OR Prn OR Prep OR CnjCoo OR CnjSub OR CnjAdv OR Rel OR Interj ;
SET NP_Member = N OR A OR Det OR PreAdv OR Adv OR Prn ; # Prn es distinto al resto...
#SET NOT_NP_Member = Word - NP_Member;
SET NOT_NP_Member = Vblex OR Vbmod OR Vbhaver OR Copula OR Prep OR CnjCoo OR CnjSub OR CnjAdv OR Rel OR Interj ;

LIST PP = (pp) ;
LIST PRS = (prs) ;
LIST Ger = (ger) ;
LIST P1 = (p1) ;
LIST P3 = (p3) ;
LIST Def = (def) ;
LIST Enc = (enc) ;
LIST Este = "Este" "este" ;
LIST Masc = m mf ;
LIST Fem = f mf ;
LIST Sg = sg sp ;
LIST Pl = pl sp ;
SET MascSg = Masc + Sg ;
SET MascPl = Masc + Pl ;
SET FemSg = Fem + Sg ;
SET FemPl = Fem + Pl ;
SET Pers = (p1) OR (p2) OR (p3) ;
SET Ant = (np ant) ;
SET Cog = (np cog) ;
SET Ant_Cog = Ant OR Cog ;
SET DetDef = (det def) ;
SET DetInd = (det ind) ;
SET Noun_Adj = N OR A ;
SET Noun_Adj_PP = N OR A OR PP ;
SET Adj_PP = A OR PP ;
SET NO_Adj_PP = Word - Adj_PP ;
SET Adv_PreAdv = Adv OR PreAdv ;
SET Verb_Prep = Verb OR Prep ;
SET Cnj_Rel = CnjCoo OR CnjSub OR CnjAdv OR Rel ;
SET Cnj_Rel_End = >>> OR ("sent") OR CM OR Cnj_Rel ;
SET Barrera_Final_SN = <<< OR Prep OR Det OR CnjSub OR CnjAdv OR Verb OR Rel OR Pro ;	# Verb és perillós per la ambigüitat, però en general, prolífic
SET Barrera_Final_Estrict_SN = <<< OR Prep OR Det OR CnjSub OR CnjAdv OR Rel OR Pro ;
SET Barrera_Inicial_SN = >>> OR Prep OR CnjSub OR CnjAdv OR Verb OR Rel ;	# Verb és perillós per la ambigüitat, però en general, prolífic
SET Barrera_Inicial_Estrict_SN = >>> OR Prep OR CnjSub OR CnjAdv OR Rel ;
SET Limite_Barrera_Inicial_SN = ("ser") OR ("Ser") OR ("estar") OR ("Estar") OR ("parecer") OR ("Parecer") OR ("haber") OR ("Haber") ;



###############################################################################
#  Morphological disambiguation
#
SECTION

####### Inari 2016 ad hoc
# ella busca
REMOVE N IF (0 Vblex) (-1 Prn) ;



REMOVE:r_bajo_1 Vblex (0 ("bajo") OR ("Bajo")) (*-1 Vblex BARRIER Cnj_Rel) ;  # no funciona sempre
REMOVE:r_bajo_2 Vblex (0 ("bajo") OR ("Bajo")) (*1 Vblex BARRIER Cnj_Rel) ;
#SELECT:bajo_3 Prep (0 ("bajo") OR ("Bajo")) (1 Det) ;
#SELECT:bajo_4 Prep (0 ("bajo") OR ("Bajo")) (-1 Copula) ;
#SELECT:bajo_5 Prep (0 ("bajo") OR ("Bajo")) (-1C N) (1C N) ;





############## 
# Pronom / Det

SELECT:i01_s_det Det IF (NOT 1C VerbFin) ;  # TOP 1
    # la casa (en que vivo)
SELECT:i08_s_pro PrnIndep IF (1C VerbFin) ; # TOP 8
    # (el novio) la casa

# just to test new feature in CG_parse
SELECT:este_2 (det) IF (1 Este OR Masc) ;
SELECT:masc_2 (det) IF (0 Det + Cog) ;

#REMOVE:i01_r_det Det IF (1C VerbFin) ;  # TOP 1
    # la casa (en que vivo)
#REMOVE:i08_r_pro PrnIndep IF (NOT 1C VerbFin) ; # TOP 8
    # (el novio) la casa

# todos son iguales / todos los días
REMOVE:i12_r_predet Predet IF (NOT 1 Det) ; # TOP 12


# desambiguació (adj|prn) itg
# qué decir? / qué tal / qué le diría
# Tie for TOP 15/16 for SATCG; chose as TOP 15 one that was less significant for VISLCG3
SELECT:i15_s_prn_1 Prn IF (0 A) (1C Verb OR Adv OR Pro) ; # TOP 15
SELECT:i16_s_prn_2 Prn IF (0 A) (-1C Prep) ; # de qué?  # TOP 16


######
# Prep

# Benefit seems even 
SELECT:i14_s_prep Prep IF (-1 Verb) (1 N) ; # TOP 14


############# 
# N / A / Adv

# Disambiguate between n, adj
SELECT:i13_s_a_adv A IF (-1C Det)  (1C N) ; # TOP 13
           # un mal día
SELECT:i02_s_n_adj N IF (-1C Det OR A OR Num OR Prep) (NOT 1 N) ; # TOP 2
#REMOVE:i02_r_n_adj A IF (-1C Det OR A OR Num OR Prep) (NOT 1 N) ; # TOP 2
           # el mal en persona
	   # las cinco españolas


SELECT:i20_s_noun N IF (0 Verb) (1 A) ;
#REMOVE:i20_r_verb Verb IF (0 N) (1 A) ;


SELECT:i06_s_adv Adv IF (1 Cnj_Rel_End OR Prep OR Det) ; # TOP 6
           # está mal (.|y no me gusta|, pero ...|en mi opinión|la nueva casa)  
SELECT:i18_s_preadv PreAdv IF (1C PP) ; # TOP 18
           # está mal hecho

# la casa era nueva / él vino ayer     # becomes worse with SELECT
REMOVE:i04_r_noun N    IF (-1 N OR Prn) ; # TOP 4
REMOVE:i19_r_verb Verb IF (-1C A OR Det) ; # TOP 19 (Removing this from 19 rules gives 83.15 -> 83.19) # but still this combined with TOP 1 is the second best combo for 2 rules

#SELECT:i20_s_noun N IF (-1 Det) (1 A) ;

### Verbs

# all verb forms that are ambiguous between 1st and 3rd person 
REMOVE:i10_r_pr_cn_p1 PastCondSubjP1 IF (-1 N OR P3) ; # TOP 10

# eliminación del imperativo en competición con el presente del indicativo
SELECT:i03_s_pri_1 (pri p3) IF (0 (vblex imp p2 sg)) ; # TOP 3

REMOVE:i09_r_imp Imp IF (0 Subj) (-1* CnjSub) ; # TOP 9


# para él / así lo veo
REMOVE:i17_r_v_pro Verb IF (1 Pro) ; # TOP 17

# comen sano
REMOVE:i07_r_v_v Verb IF (-1C Vblex) ;  # TOP 7

# para estar
REMOVE:i05_r_vfin_prep VerbFin IF (0 Prep) (1 Inf) ; # TOP 5

# de entre
REMOVE:i11_r_vfin_prep VerbFin IF (-1C Prep) ; # TOP 11






